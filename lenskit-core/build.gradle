buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.eclipse.jgit:org.eclipse.jgit:3.3.1.201403241930-r'
    }
}

apply from: "$rootDir/gradle/maven.gradle"
apply plugin: 'groovy'

dependencies {
    compile project(':lenskit-api')
    compile group: 'org.grouplens.grapht', name: 'grapht', version: '0.9.0-SNAPSHOT'
    compile group: 'com.google.guava', name: 'guava', version: '16.0.1'

    compile group: 'net.mikera', name: 'vectorz', version: '0.29.0'

    testCompile project(':lenskit-test')
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: groovyVersion
}

meta {
    name 'LensKit Core'
    description 'The core of LensKit, providing basic implementations and algorithm support.'
}

/* Since we define an annotation processor in this project, run it in its own task */
task checkAnnotations(type: JavaCompile, dependsOn: classes) {
    source sourceSets.main.java
    classpath = sourceSets.main.compileClasspath
    destinationDir = file("$buildDir/check-output")
    
    options.compilerArgs << '-proc:only'
}
check.dependsOn checkAnnotations

/* Generate the Git commit list */
import org.eclipse.jgit.api.Git
import org.eclipse.jgit.lib.Constants
import org.eclipse.jgit.storage.file.FileRepositoryBuilder
def withRepo(block) {
    def bld = new FileRepositoryBuilder()
    def repo = bld.readEnvironment().findGitDir().build()
    try {
        block.call(repo)
    } finally {
        repo.close()
    }
}

task listGitCommits(group: 'build') {
    // where does the report go?
    ext.outputFile = "$sourceSets.main.output.resourcesDir/META-INF/lenskit/git-commits.lst"
    // make sure we don't get stomped on
    mustRunAfter processResources
    
    outputs.file outputFile
    // we change with the HEAD revision
    inputs.property('HEAD') {
        withRepo { repo ->
            repo.resolve(Constants.HEAD)
        }
    }

    doLast {
        mkdir file(outputFile).parentFile
        withRepo { repo ->
            logger.info 'git repository at {}', repo.directory
            def head = repo.resolve('HEAD')
            logger.info 'head revision: {}', head.name
            file(outputFile).withPrintWriter { out ->
                def git = new Git(repo)
                int n = 0
                for (commit in git.log().add(repo.resolve(Constants.HEAD)).call()) {
                    logger.debug 'found commit {}', commit.id
                    out.println commit.id.name
                    n += 1
                }
                logger.info 'found {} revisions', n
            }
        }
    }
}
classes.dependsOn listGitCommits
