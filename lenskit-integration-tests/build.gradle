dependencies {
    compile project(':lenskit-all')
    compile project(':lenskit-test')
    testRuntime project(':lenskit-cli')
}

apply plugin: 'groovy'

// These tests require the MovieLens data set - arrange to download it
ext.mlDataDir = file("$buildDir/ml-100k")
ext.mlDataFile = file("$buildDir/ml-100k.zip")
ext.mlDataURL = 'http://files.grouplens.org/datasets/movielens/ml-100k.zip'

task fetchData(group: 'build') {
    description "Fetch data for integration tests."
    inputs.property 'url', mlDataURL
    outputs.file mlDataFile
    outputs.dir mlDataDir
    // don't download if we're running offline
    onlyIf { !gradle.startParameter.isOffline() }
}
fetchData << {
    logger.lifecycle "This analysis makes use of the MovieLens 100K data set from GroupLens Research.  Use of this data set is restricted to non-commercial purposes and is only permitted in accordance with the license terms.  More information is available at <http://lenskit.grouplens.org/ML100K>."
    mkdir buildDir
    ant {
        get src: mlDataURL, dest: mlDataFile, skipexisting: true
        unzip(src: mlDataFile, dest: mlDataDir) {
            mapper type: 'flatten'
        }
    }
}

// Configure the tests to use the data set
test {
    dependsOn fetchData
    systemProperties 'lenskit.movielens.100k': mlDataDir
}

// Run some high-level integration tests
task runEvalTest(type: JavaExec, group: 'test') {
    description 'Run an evaluation script for testing.'
    dependsOn fetchData
    mustRunAfter test // run the JUnit-based tests first, less overhead
    
    ext.sourceDir = 'src/it/all-output-files'
    ext.testDir = "$buildDir/it/eval-script"

    inputs.dir sourceDir
    inputs.dir mlDataDir
    outputs.dir testDir

    // before running the eval script, copy it to the working dir
    doFirst {
        delete testDir
        copy {
            from sourceDir
            into testDir
            include '*.groovy'
        }
    }

    workingDir testDir
    classpath configurations.testRuntime
    main 'org.grouplens.lenskit.cli.Main'
    args 'eval', '-j0'
    // tell it where to find the data
    args "-Dlenskit.movielens.100k=$mlDataDir"
}

task verifyEvalTest(type: JavaExec, group: 'test') {
    description 'Verify the run evaluation script.'
    dependsOn runEvalTest

    inputs.dir runEvalTest.testDir

    workingDir runEvalTest.testDir
    classpath sourceSets.test.runtimeClasspath
    main 'org.grouplens.lenskit.test.VerifyTestRun'
    args runEvalTest.testDir
}
check.dependsOn verifyEvalTest
