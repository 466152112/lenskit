allprojects {
    group 'org.grouplens.lenskit'
    version '2.1-SNAPSHOT'
    
    ext.groovyVersion = '2.1.5'
    ext.logbackVersion = '1.0.13'
    ext.slf4jVersion = '1.7.6'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = '1.6'

    ext.inTravis = System.getenv("CI") != null

    compileJava {
        options.encoding = 'UTF-8'
        options.fork = true
        options.deprecation = true
        options.compilerArgs << '-Xlint' << '-Xlint:-processing'
    }

    // some common deps shared by everything
    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
        compile group: 'com.google.code.findbugs', name: 'annotations', version: '2.0.1'
    }

    if (name != 'lenskit-test') {
        dependencies {
            testCompile project(':lenskit-test')
            testRuntime group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
        }
        test {
            systemProperties listener: 'org.grouplens.lenskit.util.test.LoggingListener'
            systemProperties 'log.file': "$buildDir/test.log"
        }
    }
}

// Impose some ordering on the tasks to make output slightly saner
def order(first, second) {
    project(second).afterEvaluate { p ->
        p.tasks['compileJava'].mustRunAfter ":$first:check"
        p.tasks['compileJava'].mustRunAfter ":$first:build"
    }
}

order 'lenskit-test', 'lenskit-data-structures'
order 'lenskit-data-structures', 'lenskit-api'
order 'lenskit-api', 'lenskit-core'
order 'lenskit-core', 'lenskit-groovy'
order 'lenskit-groovy', 'lenskit-eval'
order 'lenskit-groovy', 'lenskit-knn'
order 'lenskit-groovy', 'lenskit-svd'
order 'lenskit-groovy', 'lenskit-slopeone'
order 'lenskit-groovy', 'lenskit-predict'
order 'lenskit-eval', 'lenskit-all'
order 'lenskit-all', 'lenskit-cli'
