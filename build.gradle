// inspired by http://blog.freeside.co/post/48926142330/using-sass-and-compass-with-gradle

buildscript {
    repositories {
        mavenLocal()
        maven {
            url 'https://elehack.net/maven/'
        }
    }
    dependencies {
        classpath 'net.elehack.gradle:gradle-jruby:0.1'
    }
}

import net.elehack.gradle.jruby.*

apply plugin: 'jruby'

configurations.jruby.dependencies.clear()
jruby {
    version '1.7.11'
}
dependencies {
    jruby 'org.jruby:jruby:1.7.11'
}

repositories {
    mavenCentral()
}

def getDeployTarget() {
    if (!hasProperty('deployPath')) {
        throw new RuntimeException("no deploy target specified")
    }

    if (hasProperty('deployHost')) {
        return "${deployHost}:${deployPath}"
    } else {
        return deployPath
    }
}

task site(type: GemExec, group: 'build') {
    description "Build the web site."
    script 'nanoc'
    args 'compile'
}

task deploy(type: Exec, dependsOn: site) {
    executable = 'rsync'
    args '-vrp', '--chmod=Dg+s,ug+w,Fo-w,+X'
    args 'output/'
    args deployTarget
}

task debugJRuby << {
    project.javaexec {
        main 'org.jruby.Main'
        classpath project.configurations.jruby
        environment.remove('GEM_HOME')
        environment 'GEM_PATH', project.file(project.jruby.bootstrapPath)
        args '-e', "print 'gem path: ' + ENV['GEM_PATH']"
        args '-e', "print 'gem home: ' + ENV['GEM_HOME']"
    }
}

task clean(type: Delete) {
    delete 'output', 'tmp'
}

task veryclean(type: Delete, dependsOn: clean) {
    description "Remove boostrapped stuff"
    delete jruby.gemRoot
}
